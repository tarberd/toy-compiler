use crate::ast::*;

grammar;

pub Module: ModuleStatement = {
    StatementList => {
        ModuleStatement {
            id: Identifier { value: "root".to_string() },
            statements: <>,
        }
    },
};

StatementList: Vec<Statement> = {
    Statement* => <>,
};

Statement: Statement = {
    <s: ExternFunctionDeclaration> ";" => Statement::ExternFunctionDeclaration(Box::new(s)),
    <s: FunctionDefinition> ";" => Statement::FunctionDefinition(Box::new(s)),
    <s: VariableDefinition> ";" => Statement::VariableDefinition(Box::new(s)),
    <s: Return> ";" => Statement::Return(Box::new(s)),
};

ExternFunctionDeclaration: ExternFunctionDeclarationStatement = {
    "extern" "fn" <id: Identifier> "(" <parameters: ParameterList?> ")" ":" <return_type: Type> => {
        let parameters = match parameters {
            Some(parameters) => parameters,
            None => vec![],
        };

        ExternFunctionDeclarationStatement { id, parameters, return_type }
    },
};

FunctionDefinition: FunctionDefinitionStatement = {
    "fn" <id: Identifier> "(" <parameters: ParameterList?>  ")" ":" <return_type: Type> "=>" <body: Expression> => {
        let parameters = match parameters {
            Some(parameters) => parameters,
            None => vec![],
        };

        FunctionDefinitionStatement { id, parameters, return_type, body }
    },
};

ParameterList: Vec<(Identifier, Type)> = {
    <mut id_list: (Parameter ",")*>  <id: Parameter> ","? => {
        let mut id_list: Vec<(Identifier, Type)> = id_list.drain(..).map(|x| x.0).collect();
        id_list.push(id);
        id_list
    },
};

Parameter: (Identifier, Type) = {
    <id: Identifier> ":" <type_: Type> => (id, type_),
};

VariableDefinition: VariableDefinitionStatement = {
    "let" <id: Identifier> ":" <type_: Type> "=" <initialize_expression: Expression> => {
        VariableDefinitionStatement { id, type_, initialize_expression }
    },
};

Return: ReturnStatement = {
    "return" <e: Expression> => {
        ReturnStatement {
            expression: e,
        }
    },
};

Type: Type = {
    BooleanType,
    IntType => Type::Int(<>),
    UIntType => Type::UInt(<>),
};

IntType: IntType = {
    "i8" => IntType::I8,
    "i16" => IntType::I16,
    "i32" => IntType::I32,
    "i64" => IntType::I64,
    "isize" => IntType::ISize,
};

UIntType: UIntType = {
    "u8" => UIntType::U8,
    "u16" => UIntType::U16,
    "u32" => UIntType::U32,
    "u64" => UIntType::U64,
    "usize" => UIntType::USize,
};

BooleanType: Type = {
    "bool" => Type::Boolean,
};

Identifier: Identifier = {
    r"[_[:alpha:]][_[:alnum:]]*" => Identifier { value: <>.to_string() },
};

Expression: Expression = {
    BinaryBooleanExpression,
};

BinaryBooleanExpression: Expression = {
    <left: BinaryBooleanExpression> <op: BinaryBooleanOperator> <right: UnaryBooleanExpression> => {
        let bi_op = BinaryExpression {
            operator: op,
            left: left,
            right: right,
        };

        Expression::Binary(Box::new(bi_op))
    },
    UnaryBooleanExpression,
};

BinaryBooleanOperator: BinaryOperator = {
    "&&" => BinaryOperator::And,
    "and" => BinaryOperator::And,
    "||" => BinaryOperator::Or,
    "or" => BinaryOperator::Or,

    ">" => BinaryOperator::GreaterThan,
    ">=" => BinaryOperator::GreaterEqualThan,
    "==" => BinaryOperator::Equal,
    "<=" => BinaryOperator::LessEqualThan,
    "<" => BinaryOperator::LessThan,
};

UnaryBooleanExpression: Expression = {
    <op: UnaryBooleanOperator> <expression: TermExpression> => {
        let u_op = UnaryExpression {
            operator: op,
            expression: expression,
        };

        Expression::Unary(Box::new(u_op))
    },
    TermExpression,
};

UnaryBooleanOperator: UnaryOperator = {
    "!" => UnaryOperator::Not,
    "not" => UnaryOperator::Not,
};

TermExpression: Expression = {
    <left: TermExpression> <op: TermOperator> <right: FactorExpression> => {
        let bi_op = BinaryExpression {
            operator: op,
            left: left,
            right: right,
        };

        Expression::Binary(Box::new(bi_op))
    },
    FactorExpression,
};

TermOperator: BinaryOperator = {
    "+" => BinaryOperator::Plus,
    "-" => BinaryOperator::Minus,
};

FactorExpression: Expression = {
    <left: FactorExpression> <op: FactorOperator> <right: UnaryFactorExpression> => {
        let bi_op = BinaryExpression {
            operator: op,
            left: left,
            right: right,
        };

        Expression::Binary(Box::new(bi_op))
    },
    UnaryFactorExpression,
};

FactorOperator: BinaryOperator = {
    "*" => BinaryOperator::Multiplication,
    "/" => BinaryOperator::Division,
};

UnaryFactorExpression: Expression = {
    <op: UnaryFactorOperator> <expression: BlockExpression> => {
        let u_op = UnaryExpression {
            operator: op,
            expression: expression,
        };

        Expression::Unary(Box::new(u_op))
    },
    BlockExpression,
    FunctionCallExpression,
    IfExpression,
    NestedExpression,
};

UnaryFactorOperator: UnaryOperator = {
    "-" => UnaryOperator::Minus,
    "&" => UnaryOperator::Deref,
};

BlockExpression: Expression = {
    "{" <statements: StatementList> <return_expression: Expression?> "}" => {
        let block = BlockExpression {
            statements,
            return_expression,
        };

        Expression::Block(Box::new(block))
    },
};

FunctionCallExpression: Expression = {
    <callee: NestedExpression> "(" <arguments: ExpressionList> ")" => {
        let call = CallExpression {
            callee,
            arguments,
        };

        Expression::Call(Box::new(call))
    },
};

IfExpression: Expression = {
    "if" <condition: Expression> <true_path: BlockExpression> "else" <false_path: BlockExpression> => {
        Expression::If(Box::new(IfExpression {
            condition,
            true_path,
            false_path,
        }))
    },
};

ExpressionList: Vec<Expression> = {
    <mut expr_list: (Expression ",")*>  <expr: Expression> ","? => {
        let mut expr_list: Vec<Expression> = expr_list.drain(..).map(|x| x.0).collect();
        expr_list.push(expr);
        expr_list
    },
    () => vec![],
};

NestedExpression: Expression = {
    "(" <expr: Expression> ")" => expr,
    LeafExpression,
};

LeafExpression: Expression = {
    Literal => Expression::Literal(<>),
    Identifier => Expression::Identifier(<>),
};

Literal: Literal = {
    IntLiteral,
    BooleanLiteral,
};

IntLiteral: Literal = {
    <value: r"([[:digit:]]+_*)+"> <suffix: IntType> => {
        let value = value.to_string().replace("_", "");
        let literal_type = LiteralIntType::Signed(suffix);
        Literal::Int(value.parse().unwrap(), literal_type)
    },
    <value: r"([[:digit:]]+_*)+"> <suffix: UIntType> => {
        let value = value.to_string().replace("_", "");
        let literal_type = LiteralIntType::Unsigned(suffix);
        Literal::Int(value.parse().unwrap(), literal_type)
    },
    <value: r"([[:digit:]]+_*)+"> => {
        let value = value.to_string().replace("_", "");
        let literal_type = LiteralIntType::Unsufixed;
        Literal::Int(value.parse().unwrap(), literal_type)
    },
};

BooleanLiteral: Literal = {
    "true" => Literal::Boolean(true),
    "false" => Literal::Boolean(false),
}
